# stdp - Synapse model for spike-timing dependent plasticity
# ##########################################################
#
# ...
#
model liu_pan_neuromodulated_stdp_synapse:
    state:
        w real = 1.    # Synaptic weight
        wtr real = 0.
        pre_trace real = 0.
        post_trace real = 0.

    parameters:
        n real = 0.    # neuromodulator concentration between 0 and 1
        d ms = 1 ms    # Synaptic transmission delay
        beta real = 0.01  # learning rate
        tau_tr_pre ms = 20 ms
        tau_tr_post ms = 20 ms
        dA_pre real = 0.0001
        dA_post real = -0.000000105 #XXX: Taken from script (TD_STDP.py)
        wtr_max real = 0.1
        wtr_min real = 0

    # Notice: No decay for wtr
    equations:
        pre_trace' = -pre_trace / tau_tr_pre
        post_trace' = -post_trace / tau_tr_post

    input:
        pre_spikes <- spike
        post_spikes <- spike

    output:
        spike(weight real, delay ms)

    onReceive(post_spikes):
        post_trace += dA_post

        wtr += pre_trace
        wtr = max(wtr_min, wtr)
        wtr = min(wtr_max, wtr)

    onReceive(pre_spikes):
        pre_trace += dA_pre

        wtr += post_trace
        wtr = max(wtr_min, wtr)
        wtr = min(wtr_max, wtr)

        # deliver spike to postsynaptic partner
        emit_spike(w, d)

    update:
        integrate_odes()
